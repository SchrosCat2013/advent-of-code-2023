#!meta

{"kernelInfo":{"defaultKernelName":"csharp","items":[{"aliases":[],"name":"csharp"},{"aliases":[],"languageName":"fsharp","name":"fsharp"}]}}

#!markdown

# --- Day 16: The Floor Will Be Lava ---
With the beam of light completely focused somewhere, the reindeer leads you deeper still into the Lava Production Facility. At some point, you realize that the steel facility walls have been replaced with cave, and the doorways are just cave, and the floor is cave, and you're pretty sure this is actually just a giant cave.

Finally, as you approach what must be the heart of the mountain, you see a bright light in a cavern up ahead. There, you discover that the beam of light you so carefully focused is emerging from the cavern wall closest to the facility and pouring all of its energy into a contraption on the opposite side.

Upon closer inspection, the contraption appears to be a flat, two-dimensional square grid containing empty space (.), mirrors (/ and \), and splitters (| and -).

The contraption is aligned so that most of the beam bounces around the grid, but each tile on the grid converts some of the beam's light into heat to melt the rock in the cavern.

You note the layout of the contraption (your puzzle input). For example:

```
.|...\....
|.-.\.....
.....|-...
........|.
..........
.........\
..../.\\..
.-.-/..|..
.|....-|.\
..//.|....
```
The beam enters in the top-left corner from the left and heading to the right. Then, its behavior depends on what it encounters as it moves:

If the beam encounters empty space (.), it continues in the same direction.
If the beam encounters a mirror (/ or \), the beam is reflected 90 degrees depending on the angle of the mirror. For instance, a rightward-moving beam that encounters a / mirror would continue upward in the mirror's column, while a rightward-moving beam that encounters a \ mirror would continue downward from the mirror's column.
If the beam encounters the pointy end of a splitter (| or -), the beam passes through the splitter as if the splitter were empty space. For instance, a rightward-moving beam that encounters a - splitter would continue in the same direction.
If the beam encounters the flat side of a splitter (| or -), the beam is split into two beams going in each of the two directions the splitter's pointy ends are pointing. For instance, a rightward-moving beam that encounters a | splitter would split into two beams: one that continues upward from the splitter's column and one that continues downward from the splitter's column.
Beams do not interact with other beams; a tile can have many beams passing through it at the same time. A tile is energized if that tile has at least one beam pass through it, reflect in it, or split in it.

In the above example, here is how the beam of light bounces around the contraption:

```
>|<<<\....
|v-.\^....
.v...|->>>
.v...v^.|.
.v...v^...
.v...v^..\
.v../2\\..
<->-/vv|..
.|<<<2-|.\
.v//.|.v..
```
Beams are only shown on empty tiles; arrows indicate the direction of the beams. If a tile contains beams moving in multiple directions, the number of distinct directions is shown instead. Here is the same diagram but instead only showing whether a tile is energized (#) or not (.):

```
######....
.#...#....
.#...#####
.#...##...
.#...##...
.#...##...
.#..####..
########..
.#######..
.#...#.#..
```
Ultimately, in this example, 46 tiles become energized.

The light isn't energizing enough tiles to produce lava; to debug the contraption, you need to start by analyzing the current situation. With the beam starting in the top-left heading right, how many tiles end up being energized?

#!fsharp

#!import shared.fs
open Shared

let sampleInput =
    [|
        @".|...\...."
        @"|.-.\....."
        @".....|-..."
        @"........|."
        @".........."
        @".........\"
        @"..../.\\.."
        @".-.-/..|.."
        @".|....-|.\"
        @"..//.|...."
    |]
    |> array2D

sampleInput |> Array2D.renderChars

#!fsharp

[<Struct>]
type BeamHead = {
    loc: Point
    direction: Direction
}

let reflect (c: char) (d: Direction) =
    match (c, d) with
    | '/', Left -> Down
    | '/', Right -> Up
    | '/', Up -> Right
    | '/', Down -> Left
    | '\\', Left -> Up
    | '\\', Right -> Down
    | '\\', Up -> Left
    | '\\', Down -> Right
    | (_, _) -> raise (InvalidOperationException "Tried to reflect off a tile that is not a mirror")

let next (input: char [,]) (beam: BeamHead) =
    let p = beam.loc |> move beam.direction
    match input |> Array2D.tryGet p.y p.x with
    | None
        -> []
    | Some '.'
        -> [ { loc = p; direction = beam.direction } ]
    | Some '|' when (beam.direction = Up || beam.direction = Down)
        -> [ { loc = p; direction = beam.direction } ]
    | Some '-' when (beam.direction = Left || beam.direction = Right)
        -> [ { loc = p; direction = beam.direction } ]
    | Some c when (c = '/' || c = '\\')
        -> [ { loc = p; direction = beam.direction |> reflect c }]
    | Some '|'
        -> [ { loc = p; direction = Up }; { loc = p; direction = Down }]
    | Some '-'
        -> [ { loc = p; direction = Left }; { loc = p; direction = Right }]
    | Some c
        -> raise (InvalidOperationException $"Unexpected character in array { c }")

let followBeamsOneStep (input: char [,]) (beams: BeamHead list) =
    beams |> List.collect (next input)

let initialBeam = [{ loc = { x = -1; y = 0 }; direction = Right }]

let rec followBeamsTillEnd (beamHeads: BeamHead list) (visited: Set<BeamHead>) (input: char [,]) =
    if beamHeads = []
    then visited |> Set.map (fun beamHead -> beamHead.loc)
    else
        let nb =
            beamHeads
            |> List.collect (next input)
            |> List.filter (not << fun b -> visited |> Set.contains b)

        let nv = visited |> Set.union (Set.ofList nb)
        followBeamsTillEnd nb nv input

sampleInput |> followBeamsTillEnd initialBeam Set.empty

#!fsharp

let renderVisited (input: char[,]) =
    let locations = input |> followBeamsTillEnd initialBeam Set.empty
    input
    |> Array2D.mapi (fun y x c -> if locations |> Set.contains { y = y; x = x } then '#' else c)
    |> Array2D.renderChars

renderVisited sampleInput

#!fsharp

let part1 =
    followBeamsTillEnd initialBeam Set.empty
    >> Set.count

part1 sampleInput

#!fsharp

let challengeInput =
    File.ReadAllLines "day 16 input.txt"
    |> array2D

part1 challengeInput

#!fsharp

let allPossibleStartingPoints (input: _[,]) =
    let (length1, length2) = (Array2D.length1 input, Array2D.length2 input)

    seq {
        for i = 0 to length1 - 1 do
            { loc = { x = -1; y = i }; direction = Right }
            { loc = { x = length1; y = i }; direction = Left }
        for i = 0 to length2 - 1 do
            { loc = { x = i; y = -1}; direction = Down }
            { loc = { x = i; y = length2 }; direction = Up }
    }

allPossibleStartingPoints sampleInput
|> Array.ofSeq

#!fsharp

let part2 (input: char[,]) =
    allPossibleStartingPoints input
    |> Seq.map (fun beam -> followBeamsTillEnd [beam] Set.empty input |> Set.count)
    |> Seq.max

part2 sampleInput

#!fsharp

part2 challengeInput
