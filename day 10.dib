#!meta

{"kernelInfo":{"defaultKernelName":"csharp","items":[{"aliases":[],"name":"csharp"},{"aliases":[],"languageName":"fsharp","name":"fsharp"}]}}

#!markdown

# --- Day 10: Pipe Maze ---
You use the hang glider to ride the hot air from Desert Island all the way up to the floating metal island. This island is surprisingly cold and there definitely aren't any thermals to glide on, so you leave your hang glider behind.

You wander around for a while, but you don't find any people or animals. However, you do occasionally find signposts labeled "Hot Springs" pointing in a seemingly consistent direction; maybe you can find someone at the hot springs and ask them where the desert-machine parts are made.

The landscape here is alien; even the flowers and trees are made of metal. As you stop to admire some metal grass, you notice something metallic scurry away in your peripheral vision and jump into a big pipe! It didn't look like any animal you've ever seen; if you want a better look, you'll need to get ahead of it.

Scanning the area, you discover that the entire field you're standing on is densely packed with pipes; it was hard to tell at first because they're the same metallic silver color as the "ground". You make a quick sketch of all of the surface pipes you can see (your puzzle input).

The pipes are arranged in a two-dimensional grid of tiles:

```
| is a vertical pipe connecting north and south.
- is a horizontal pipe connecting east and west.
L is a 90-degree bend connecting north and east.
J is a 90-degree bend connecting north and west.
7 is a 90-degree bend connecting south and west.
F is a 90-degree bend connecting south and east.
. is ground; there is no pipe in this tile.
S is the starting position of the animal; there is a pipe on this tile, but your sketch doesn't show what shape the pipe has.
```

Based on the acoustics of the animal's scurrying, you're confident the pipe that contains the animal is one large, continuous loop.

For example, here is a square loop of pipe:

```
.....
.F-7.
.|.|.
.L-J.
.....
```
If the animal had entered this loop in the northwest corner, the sketch would instead look like this:

```
.....
.S-7.
.|.|.
.L-J.
.....
```
In the above diagram, the S tile is still a 90-degree F bend: you can tell because of how the adjacent pipes connect to it.

Unfortunately, there are also many pipes that aren't connected to the loop! This sketch shows the same loop as above:

```
-L|F7
7S-7|
L|7||
-L-J|
L|-JF
```

In the above diagram, you can still figure out which pipes form the main loop: they're the ones connected to S, pipes those pipes connect to, pipes those pipes connect to, and so on. Every pipe in the main loop connects to its two neighbors (including S, which will have exactly two pipes connecting to it, and which is assumed to connect back to those two pipes).

Here is a sketch that contains a slightly more complex main loop:

```
..F7.
.FJ|.
SJ.L7
|F--J
LJ...
```
Here's the same example sketch with the extra, non-main-loop pipe tiles also shown:

```
7-F7-
.FJ|7
SJLL7
|F--J
LJ.LJ
```

If you want to get out ahead of the animal, you should find the tile in the loop that is farthest from the starting position. Because the animal is in the pipe, it doesn't make sense to measure this by direct distance. Instead, you need to find the tile that would take the longest number of steps along the loop to reach from the starting point - regardless of which way around the loop the animal went.

In the first example with the square loop:

```
.....
.S-7.
.|.|.
.L-J.
.....
```
You can count the distance each tile in the loop is from the starting point like this:

```
.....
.012.
.1.3.
.234.
.....
```
In this example, the farthest point from the start is 4 steps away.

Here's the more complex loop again:

```
..F7.
.FJ|.
SJ.L7
|F--J
LJ...
```
Here are the distances for each tile on that loop:

```
..45.
.236.
01.78
14567
23...
```

Find the single giant loop starting at S. How many steps along the loop does it take to get from the starting position to the point farthest from the starting position?

#!fsharp

#!import shared.fs
open Shared

type Pipe =
    | NorthSouth
    | EastWest
    | NorthEast
    | NorthWest
    | SouthEast
    | SouthWest

type ChallengeInput = {
    pipes: Pipe option[,]
    start: Point
}

let parseInput (input: string array) =
    let inputChars = array2D input

    let isCharacterIn (chars: string) (y: int) (x: int) =
        match inputChars |> Array2D.tryGet y x with
        | Some c -> chars.Contains(c)
        | None -> false

    let determinePipeForStartingLocation (y: int) (x: int) =
        match (
            isCharacterIn "F-L" y (x-1),
            isCharacterIn "7-J" y (x+1),
            isCharacterIn "F|7" (y-1) x,
            isCharacterIn "L|J" (y+1) x
        ) with
        | (true, true, false, false) -> EastWest
        | (false, false, true, true) -> NorthSouth
        | (true, false, true, false) -> NorthWest
        | (true, false, false, true) -> SouthWest
        | (false, true, true, false) -> NorthEast
        | (false, true, false, true) -> SouthEast
        | _ -> raise (InvalidOperationException "Could not determine orientation of starting pipe")

    let conversion y x = function
        | '|' -> Some NorthSouth
        | '-' -> Some EastWest
        | 'F' -> Some SouthEast
        | '7' -> Some SouthWest
        | 'L' -> Some NorthEast
        | 'J' -> Some NorthWest
        | 'S' -> Some <| determinePipeForStartingLocation y x
        | '.' -> None
        | c -> raise (InvalidOperationException $"Could not convert caracter { c } at location y={y} x={x}")

    {
        pipes =
            inputChars
            |> Array2D.mapi conversion
        start =
            match inputChars |> Array2D.tryFindIndex ((=) 'S') with
            | Some (y, x) -> { x = x; y = y }
            | None -> { x = 0; y = 0 }
    }

let sampleInput1 =
    [|
        "....."
        ".F-7."
        ".|.|."
        ".L-J."
        "....."
    |]
    |> parseInput

let renderGrid (pipes: Pipe option[,]) =
    let renderCharacter = function
        | Some NorthSouth -> '\u2551'
        | Some EastWest -> '\u2550'
        | Some NorthEast -> '\u255a'
        | Some NorthWest -> '\u255d'
        | Some SouthEast -> '\u2554'
        | Some SouthWest -> '\u2557'
        | None -> ' '

    pipes
    |> Array2D.map renderCharacter
    |> Array2D.renderChars

renderGrid sampleInput1.pipes

#!fsharp

let sampleInput2 =
    [|
        "7-F7-"
        ".FJ|7"
        "SJLL7"
        "|F--J"
        "LJ.LJ"
    |]
    |> parseInput

renderGrid sampleInput2.pipes

#!fsharp

sampleInput2.start

#!fsharp

let nextPoints (input: ChallengeInput) (point: Point) =
    match input.pipes[point.y, point.x] with
    | Some NorthSouth -> [| { point with y = point.y - 1 }; { point with y = point.y + 1} |]
    | Some EastWest -> [| { point with x = point.x - 1 }; { point with x = point.x + 1} |]
    | Some NorthWest -> [| { point with y = point.y - 1 }; { point with x = point.x - 1} |]
    | Some NorthEast -> [| { point with y = point.y - 1 }; { point with x = point.x + 1} |]
    | Some SouthWest -> [| { point with y = point.y + 1 }; { point with x = point.x - 1} |]
    | Some SouthEast -> [| { point with y = point.y + 1 }; { point with x = point.x + 1} |]
    | None -> raise (InvalidOperationException "Not a pipe")

let rec path (input: ChallengeInput) (prev: Point) (point: Point) =
    if point = input.start
    then Seq.singleton prev
    else
        let nextPoint =
            point
            |> nextPoints input
            |> Array.find ((<>) prev)

        seq {
            yield prev
            yield! path input point nextPoint
        }

let paths (input: ChallengeInput) =
    nextPoints input input.start
    |> Array.map (path input input.start)

paths sampleInput2

#!fsharp

let countFurthestPoint (paths: Point seq array) =
    let isLastPair (index, ((prev1, prev2), (point1, point2))) =
        index > 0 && (prev1 = prev2 || prev1 = point2 || prev2 = point1)

    match paths with
    | [| path1; path2 |] ->
        Seq.zip path1 path2
        |> Seq.pairwise
        |> Seq.indexed
        |> Seq.takeWhile (not << isLastPair)
        |> Seq.length
    | _ -> raise (InvalidOperationException "More than two paths passed to deduplicate function")

paths sampleInput2
|> countFurthestPoint

#!fsharp

let challengeInput =
    File.ReadAllLinesAsync "day 10 input.txt"
    |> Async.AwaitTask
    |> Async.RunSynchronously
    |> parseInput

renderGrid challengeInput.pipes

#!fsharp

paths challengeInput
|> countFurthestPoint

#!markdown

## --- Part Two ---
You quickly reach the farthest point of the loop, but the animal never emerges. Maybe its nest is within the area enclosed by the loop?

To determine whether it's even worth taking the time to search for such a nest, you should calculate how many tiles are contained within the loop. For example:

```
...........
.S-------7.
.|F-----7|.
.||.....||.
.||.....||.
.|L-7.F-J|.
.|..|.|..|.
.L--J.L--J.
...........
```
The above loop encloses merely four tiles - the two pairs of . in the southwest and southeast (marked I below). The middle . tiles (marked O below) are not in the loop. Here is the same loop again with those regions marked:

```
...........
.S-------7.
.|F-----7|.
.||OOOOO||.
.||OOOOO||.
.|L-7OF-J|.
.|II|O|II|.
.L--JOL--J.
.....O.....
```
In fact, there doesn't even need to be a full tile path to the outside for tiles to count as outside the loop - squeezing between pipes is also allowed! Here, I is still within the loop and O is still outside the loop:

```
..........
.S------7.
.|F----7|.
.||OOOO||.
.||OOOO||.
.|L-7F-J|.
.|II||II|.
.L--JL--J.
..........
```
In both of the above examples, 4 tiles are enclosed by the loop.

Here's a larger example:

```
.F----7F7F7F7F-7....
.|F--7||||||||FJ....
.||.FJ||||||||L7....
FJL7L7LJLJ||LJ.L-7..
L--J.L7...LJS7F-7L7.
....F-J..F7FJ|L7L7L7
....L7.F7||L7|.L7L7|
.....|FJLJ|FJ|F7|.LJ
....FJL-7.||.||||...
....L---J.LJ.LJLJ...
```
The above sketch has many random bits of ground, some of which are in the loop (I) and some of which are outside it (O):

```
OF----7F7F7F7F-7OOOO
O|F--7||||||||FJOOOO
O||OFJ||||||||L7OOOO
FJL7L7LJLJ||LJIL-7OO
L--JOL7IIILJS7F-7L7O
OOOOF-JIIF7FJ|L7L7L7
OOOOL7IF7||L7|IL7L7|
OOOOO|FJLJ|FJ|F7|OLJ
OOOOFJL-7O||O||||OOO
OOOOL---JOLJOLJLJOOO
```
In this larger example, 8 tiles are enclosed by the loop.

Any tile that isn't part of the main loop can count as being enclosed by the loop. Here's another example with many bits of junk pipe lying around that aren't connected to the main loop at all:

```
FF7FSF7F7F7F7F7F---7
L|LJ||||||||||||F--J
FL-7LJLJ||||||LJL-77
F--JF--7||LJLJ7F7FJ-
L---JF-JLJ.||-FJLJJ7
|F|F-JF---7F7-L7L|7|
|FFJF7L7F-JF7|JL---7
7-L-JL7||F7|L7F-7F7|
L.L7LFJ|||||FJL7||LJ
L7JLJL-JLJLJL--JLJ.L
```
Here are just the tiles that are enclosed by the loop marked with I:

```
FF7FSF7F7F7F7F7F---7
L|LJ||||||||||||F--J
FL-7LJLJ||||||LJL-77
F--JF--7||LJLJIF7FJ-
L---JF-JLJIIIIFJLJJ7
|F|F-JF---7IIIL7L|7|
|FFJF7L7F-JF7IIL---7
7-L-JL7||F7|L7F-7F7|
L.L7LFJ|||||FJL7||LJ
L7JLJL-JLJLJL--JLJ.L
```
In this last example, 10 tiles are enclosed by the loop.

Figure out whether you have time to search for the nest by calculating the area within the loop. How many tiles are enclosed by the loop?

#!markdown

This is a geometry bounds question. Let's use horizontal rays to determine if a point is inside the pipe.

That means we need the vertical line segments of the pipe to count.

Figure out all the vertical line segments of the pipe. We can use the path calculated earlier to determine this

#!fsharp

type VerticalLineSegment = {
    x: int
    y1: int
    y2: int
}

let verticalSegments (input: ChallengeInput) =
    let folder (segmentStart: Point, segments: VerticalLineSegment list) (prev: Point, next: Point) =
        // Moving vertically, haven't reached the end of the segment yet
        if next.x = segmentStart.x
        then (segmentStart, segments)
        else if prev.y = segmentStart.y
        // Moving horizontally
        then (next, segments)
        else
            let segment = {
                x = segmentStart.x
                y1 = min segmentStart.y prev.y
                y2 = max segmentStart.y prev.y
            }
            (next, segment::segments)

    // Add the starting point to the end of the path to complete it
    let path =
        paths input
        |> Array.item 0
        |> Seq.append [| input.start |]

    path
    |> Seq.pairwise
    |> Seq.fold folder (input.start, [])
    |> snd

paths sampleInput2 |> Array.item 0 |> Seq.append [| sampleInput2.start |]

#!markdown

Eyeballing this, we can see the following vertical segments
```
x=0, y=2..4
x=1, y=3..4
x=4, y=2..3
x=3, y=0..2
x=2, y=0..1
x=1, y=1..2
```

and the following horizontal segments
```
x=0..1, y=4
x=1..4, y=3
x=3..4, y=2
x=2..3, y=0
x=1..2, y=1
x=0..1, y=2
```

#!fsharp

verticalSegments sampleInput2

#!markdown

Not working - let's try again

#!fsharp

type Orientation =
    | Horizontal
    | Vertical

type Segment = {
    x1: int
    y1: int
    x2: int
    y2: int
    orientation: Orientation
}

let orientation (p1: Point) (p2: Point) =
    if p1.x = p2.x && p1.y = p2.y
    then raise (InvalidOperationException "Points are identical")
    else if p1.x = p2.x then Vertical
    else if p1.y = p2.y then Horizontal
    else raise (InvalidOperationException "Points are diagonal")

let rec segmentsForPath (path: Span<Point>) (prev: Point) (segmentStart: Point) (o: Orientation) =
    // Is this the last point in the segment?
    if path.IsEmpty
    then
        Seq.singleton {
            x1 = min segmentStart.x prev.x
            y1 = min segmentStart.y prev.y
            x2 = max segmentStart.x prev.x
            y2 = max segmentStart.y prev.y
            orientation = o
        }
    else
        let nextOrientation = orientation path[0] prev
        if nextOrientation = o
        then segmentsForPath (path.Slice 1) path[0] segmentStart o
        else
            let segment = {
                x1 = min segmentStart.x prev.x
                y1 = min segmentStart.y prev.y
                x2 = max segmentStart.x prev.x
                y2 = max segmentStart.y prev.y
                orientation = o
            }
            segmentsForPath (path.Slice 1) path[0] prev nextOrientation
            |> Seq.append (Seq.singleton segment)

let segmentsForInput (input: ChallengeInput) =
    let path =
        Seq.singleton input.start
        |> Seq.append ((paths input)[0] |> Seq.skip 1)
        |> Array.ofSeq
    
    segmentsForPath (path.AsSpan()) input.start input.start (orientation path[0] input.start)

let printSegment segment =
    match segment.orientation with
    | Horizontal -> $"x={segment.x1}..{segment.x2}, y={segment.y1}"
    | Vertical -> $"x={segment.x1}, y={segment.y1}..{segment.y2}"

segmentsForInput sampleInput2
|> Seq.groupBy (fun x -> x.orientation)
|> Seq.map (snd >> Seq.map printSegment)

#!markdown

comparing again:
```
x=0, y=2..4
x=1, y=3..4
x=4, y=2..3
x=3, y=0..2
x=2, y=0..1
x=1, y=1..2
```

and the following horizontal segments
```
x=0..1, y=4
x=1..4, y=3
x=3..4, y=2
x=2..3, y=0
x=1..2, y=1
x=0..2, y=2
```

#!fsharp

let countVerticalSegmentsLeftOf (segments: Segment array) (y: int) (x: int) =
    segments
    |> Seq.filter (fun seg -> x > seg.x1 && y >= seg.y1 && y < seg.y2)
    |> Seq.length

let countNestPossibilities (input: ChallengeInput) =
    let verticalSegments =
        segmentsForInput input
        |> Seq.filter (fun x -> x.orientation = Horizontal)
        |> Array.ofSeq

    let isNest (y: int) (x: int) (item: Option<_>) =
        item.IsNone
        && (countVerticalSegmentsLeftOf verticalSegments y x) % 2 = 1

    input.pipes
    |> Array2D.countWherei isNest

countNestPossibilities challengeInput
