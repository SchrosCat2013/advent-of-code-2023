#!meta

{"kernelInfo":{"defaultKernelName":"fsharp","items":[{"aliases":[],"languageName":"fsharp","name":"fsharp"}]}}

#!markdown

# --- Day 13: Point of Incidence ---
With your help, the hot springs team locates an appropriate spring which launches you neatly and precisely up to the edge of Lava Island.

There's just one problem: you don't see any lava.

You do see a lot of ash and igneous rock; there are even what look like gray mountains scattered around. After a while, you make your way to a nearby cluster of mountains only to discover that the valley between them is completely full of large mirrors. Most of the mirrors seem to be aligned in a consistent way; perhaps you should head in that direction?

As you move through the valley of mirrors, you find that several of them have fallen from the large metal frames keeping them in place. The mirrors are extremely flat and shiny, and many of the fallen mirrors have lodged into the ash at strange angles. Because the terrain is all one color, it's hard to tell where it's safe to walk or where you're about to run into a mirror.

You note down the patterns of ash (.) and rocks (#) that you see as you walk (your puzzle input); perhaps by carefully analyzing these patterns, you can figure out where the mirrors are!

For example:

```
#.##..##.
..#.##.#.
##......#
##......#
..#.##.#.
..##..##.
#.#.##.#.
```

```
#...##..#
#....#..#
..##..###
#####.##.
#####.##.
..##..###
#....#..#
```

To find the reflection in each pattern, you need to find a perfect reflection across either a horizontal line between two rows or across a vertical line between two columns.

In the first pattern, the reflection is across a vertical line between two columns; arrows on each of the two columns point at the line between the columns:

```
123456789
    ><   
#.##..##.
..#.##.#.
##......#
##......#
..#.##.#.
..##..##.
#.#.##.#.
    ><   
123456789
```

In this pattern, the line of reflection is the vertical line between columns 5 and 6. Because the vertical line is not perfectly in the middle of the pattern, part of the pattern (column 1) has nowhere to reflect onto and can be ignored; every other column has a reflected column within the pattern and must match exactly: column 2 matches column 9, column 3 matches 8, 4 matches 7, and 5 matches 6.

The second pattern reflects across a horizontal line instead:

```
1 #...##..# 1
2 #....#..# 2
3 ..##..### 3
4v#####.##.v4
5^#####.##.^5
6 ..##..### 6
7 #....#..# 7
```

This pattern reflects across the horizontal line between rows 4 and 5. Row 1 would reflect with a hypothetical row 8, but since that's not in the pattern, row 1 doesn't need to match anything. The remaining rows match: row 2 matches row 7, row 3 matches row 6, and row 4 matches row 5.

To summarize your pattern notes, add up the number of columns to the left of each vertical line of reflection; to that, also add 100 multiplied by the number of rows above each horizontal line of reflection. In the above example, the first pattern's vertical line has 5 columns to its left and the second pattern's horizontal line has 4 rows above it, a total of 405.

Find the line of reflection in each of the patterns in your notes. What number do you get after summarizing all of your notes?

#!markdown

I'm just going to work with string inputs in this challenge

#!fsharp

#!import shared.fs

type Area = Square[,]

type ChallengeInput = Area array

let parseArea (input: string seq) =
    array2D input

let sampleInput1 =
    [|
        "#.##..##."
        "..#.##.#."
        "##......#"
        "##......#"
        "..#.##.#."
        "..##..##."
        "#.#.##.#."
    |]
    |> parseArea

let sampleInput2 =
    [|
        "#...##..#"
        "#....#..#"
        "..##..###"
        "#####.##."
        "#####.##."
        "..##..###"
        "#....#..#"
    |]
    |> parseArea

let challengeInput =
    File.ReadAllLines "day 13 input.txt"
    |> Seq.splitBy ""
    |> Seq.map parseArea
    |> Array.ofSeq

challengeInput[0]
|> Array2D.renderChars

#!markdown

Test out the new rev1 and rev2 functions - these look like they're working

#!fsharp

sampleInput1[0..2,*] |> Array2D.rev1 |> Array2D.renderChars

#!fsharp

sampleInput1[0..2,*] |> Array2D.rev2 |> Array2D.renderChars

#!fsharp

let isHorizontalMirror (comparator: char seq->char seq->bool) (input: char[,]) (y: int) =
    let linesToConsider = min y ((Array2D.length1 input) - y)

    let top = input[y-linesToConsider..y-1,*]
    let bottom = input[y..y+linesToConsider-1,*] |> Array2D.rev1

    comparator (Seq.cast top) (Seq.cast bottom)

let findHorizontalMirrors (comparator: char seq->char seq->bool) (input: char[,]) =
    seq { 1..(Array2D.length1 input)-1 }
    |> Seq.tryFind (isHorizontalMirror comparator input)

sampleInput2 |> findHorizontalMirrors Seq.equal

#!fsharp

let isVerticalMirror (comparator: char seq->char seq->bool) (input: char[,]) (x: int) =
    let colsToConsider = min x ((Array2D.length2 input) - x)

    let left = input[*,x-colsToConsider..x-1]
    let right = input[*,x..x+colsToConsider-1] |> Array2D.rev2

    comparator (Seq.cast left) (Seq.cast right)

let findVerticalMirrors (comparator: char seq->char seq->bool) (input: char[,]) =
    seq { 1..(Array2D.length2 input)-1 }
    |> Seq.tryFind (isVerticalMirror comparator input)

sampleInput1 |> findVerticalMirrors Seq.equal

#!fsharp

let reflectionNumber (comparator: char seq->char seq->bool) (input: char[,]) =
    match (findHorizontalMirrors comparator input, findVerticalMirrors comparator input) with
    | Some h, None -> h * 100
    | None, Some v -> v
    | _ ->
        printfn "Could not find mirror for input:"
        Array2D.renderChars input
        raise (InvalidOperationException "Could not find mirror for input")

[| sampleInput1; sampleInput2 |]
|> Array.map (reflectionNumber Seq.equal)

#!fsharp

let part1 = Seq.map (reflectionNumber Seq.equal) >> Seq.sum

part1 challengeInput

#!fsharp

let smudgeComparer input1 input2 =
    (input1, input2)
    ||> Seq.zip
    |> Seq.filter (fun (a, b) -> a <> b)
    |> Seq.length
    |> (=) 1

[| sampleInput1; sampleInput2 |]
|> Array.map (reflectionNumber smudgeComparer)

#!fsharp

let part2 = Seq.map (reflectionNumber smudgeComparer) >> Seq.sum

part2 challengeInput
