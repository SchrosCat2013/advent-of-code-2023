#!meta

{"kernelInfo":{"defaultKernelName":"csharp","items":[{"aliases":[],"name":"csharp"},{"aliases":[],"languageName":"fsharp","name":"fsharp"}]}}

#!markdown

# --- Day 5: If You Give A Seed A Fertilizer ---
You take the boat and find the gardener right where you were told he would be: managing a giant "garden" that looks more to you like a farm.

"A water source? Island Island is the water source!" You point out that Snow Island isn't receiving any water.

"Oh, we had to stop the water because we ran out of sand to filter it with! Can't make snow with dirty water. Don't worry, I'm sure we'll get more sand soon; we only turned off the water a few days... weeks... oh no." His face sinks into a look of horrified realization.

"I've been so busy making sure everyone here has food that I completely forgot to check why we stopped getting more sand! There's a ferry leaving soon that is headed over in that direction - it's much faster than your boat. Could you please go check it out?"

You barely have time to agree to this request when he brings up another. "While you wait for the ferry, maybe you can help us with our food production problem. The latest Island Island Almanac just arrived and we're having trouble making sense of it."

The almanac (your puzzle input) lists all of the seeds that need to be planted. It also lists what type of soil to use with each kind of seed, what type of fertilizer to use with each kind of soil, what type of water to use with each kind of fertilizer, and so on. Every type of seed, soil, fertilizer and so on is identified with a number, but numbers are reused by each category - that is, soil 123 and fertilizer 123 aren't necessarily related to each other.

For example:
```
seeds: 79 14 55 13

seed-to-soil map:
50 98 2
52 50 48

soil-to-fertilizer map:
0 15 37
37 52 2
39 0 15

fertilizer-to-water map:
49 53 8
0 11 42
42 0 7
57 7 4

water-to-light map:
88 18 7
18 25 70

light-to-temperature map:
45 77 23
81 45 19
68 64 13

temperature-to-humidity map:
0 69 1
1 0 69

humidity-to-location map:
60 56 37
56 93 4
```

The almanac starts by listing which seeds need to be planted: seeds 79, 14, 55, and 13.

The rest of the almanac contains a list of maps which describe how to convert numbers from a source category into numbers in a destination category. That is, the section that starts with seed-to-soil map: describes how to convert a seed number (the source) to a soil number (the destination). This lets the gardener and his team know which soil to use with which seeds, which water to use with which fertilizer, and so on.

Rather than list every source number and its corresponding destination number one by one, the maps describe entire ranges of numbers that can be converted. Each line within a map contains three numbers: the destination range start, the source range start, and the range length.

Consider again the example seed-to-soil map:

```
50 98 2
52 50 48
```

The first line has a destination range start of 50, a source range start of 98, and a range length of 2. This line means that the source range starts at 98 and contains two values: 98 and 99. The destination range is the same length, but it starts at 50, so its two values are 50 and 51. With this information, you know that seed number 98 corresponds to soil number 50 and that seed number 99 corresponds to soil number 51.

The second line means that the source range starts at 50 and contains 48 values: 50, 51, ..., 96, 97. This corresponds to a destination range starting at 52 and also containing 48 values: 52, 53, ..., 98, 99. So, seed number 53 corresponds to soil number 55.

Any source numbers that aren't mapped correspond to the same destination number. So, seed number 10 corresponds to soil number 10.

So, the entire list of seed numbers and their corresponding soil numbers looks like this:

```
seed  soil
0     0
1     1
...   ...
48    48
49    49
50    52
51    53
...   ...
96    98
97    99
98    50
99    51
```

With this map, you can look up the soil number required for each initial seed number:

* Seed number 79 corresponds to soil number 81.
* Seed number 14 corresponds to soil number 14.
* Seed number 55 corresponds to soil number 57.
* Seed number 13 corresponds to soil number 13.

The gardener and his team want to get started as soon as possible, so they'd like to know the closest location that needs a seed. Using these maps, find the lowest location number that corresponds to any of the initial seeds. To do this, you'll need to convert each seed number through other categories until you can find its corresponding location number. In this example, the corresponding types are:

* Seed 79, soil 81, fertilizer 81, water 81, light 74, temperature 78, humidity 78, location 82.
* Seed 14, soil 14, fertilizer 53, water 49, light 42, temperature 42, humidity 43, location 43.
* Seed 55, soil 57, fertilizer 57, water 53, light 46, temperature 82, humidity 82, location 86.
* Seed 13, soil 13, fertilizer 52, water 41, light 34, temperature 34, humidity 35, location 35.

So, the lowest location number in this example is 35.

What is the lowest location number that corresponds to any of the initial seed numbers?

#!fsharp

#!import shared.fs
open System.Text.RegularExpressions
open Shared

[<Struct>]
type Item = {
    value: int64
    itemType: string
}

type MapRange = {
    sourceStart: int64
    destinationStart: int64
    length: int64
}

type ChallengeMap = {
    sourceType: string
    destinationType: string
    ranges: MapRange array
}

type ChallengeInput = {
    initialSeeds: Item array
    maps: Map<string, ChallengeMap>
}

let parseRange (input: string) =
    match input.Split(' ') with
    | [| dest; source; len |] ->
        {
            sourceStart = int64 source
            destinationStart = int64 dest
            length = int64 len
        }
    | _ -> raise (InvalidOperationException $"Could not parse range { input }")

let mapHeaderRegex = Regex("(\w+)-to-(\w+) map:", RegexOptions.Compiled)

let parseChallengeMap (input: string array) =
    match mapHeaderRegex.Match(input[0]) with
    | regexMatch when regexMatch.Success ->
        (regexMatch.Groups[1].Value, {
            sourceType = regexMatch.Groups[1].Value
            destinationType = regexMatch.Groups[2].Value
            ranges = input[1..] |> Array.map parseRange
        })
    | _ -> raise (InvalidOperationException $"Could not parse map header { input }")

let parseInput (input: string array) =
    {
        initialSeeds =
            input[0].Substring(7).Split(' ')
            |> Array.map (fun i -> { value = int64 i; itemType = "seed" })
        maps =
            input[2..]
            |> splitBy ""
            |> Seq.map parseChallengeMap
            |> Map.ofSeq
    }

let sampleInput = parseInput [|
    "seeds: 79 14 55 13"
    ""
    "seed-to-soil map:"
    "50 98 2"
    "52 50 48"
    ""
    "soil-to-fertilizer map:"
    "0 15 37"
    "37 52 2"
    "39 0 15"
    ""
    "fertilizer-to-water map:"
    "49 53 8"
    "0 11 42"
    "42 0 7"
    "57 7 4"
    ""
    "water-to-light map:"
    "88 18 7"
    "18 25 70"
    ""
    "light-to-temperature map:"
    "45 77 23"
    "81 45 19"
    "68 64 13"
    ""
    "temperature-to-humidity map:"
    "0 69 1"
    "1 0 69"
    ""
    "humidity-to-location map:"
    "60 56 37"
    "56 93 4"
|]

sampleInput

#!markdown

Looks like that parses properly. Let's move on

#!fsharp

let inRange (value: int64) (range: MapRange) =
    value >= range.sourceStart && value < range.sourceStart + range.length

let determineNextValue (value: int64) (ranges: MapRange array) =
    match ranges |> Array.tryFind (inRange value) with
    | Some range -> value - range.sourceStart + range.destinationStart
    | None -> value

let lookupNext (mappings: Map<string, ChallengeMap>) (item: Item) =
    {
        value = determineNextValue item.value mappings[item.itemType].ranges
        itemType = mappings[item.itemType].destinationType
    }

sampleInput.initialSeeds
|> Array.map (lookupNext sampleInput.maps)

#!markdown

Looks good. Let's make a generator that shows all the intermediate stages

#!fsharp

let allStagesForSeed (mappings: Map<string, ChallengeMap>) (item: Item) =
    seq {
        let mutable current = item
        yield current
        while mappings.ContainsKey current.itemType do
            current <- (lookupNext mappings current)
            yield current
    }

sampleInput.initialSeeds
|> Array.map (allStagesForSeed sampleInput.maps >> Array.ofSeq)

#!fsharp

let finalLocation maps = allStagesForSeed maps >> Seq.last

let lowestLocation (input: ChallengeInput) =
    input.initialSeeds
    |> Seq.map (finalLocation input.maps)
    |> Seq.min

lowestLocation sampleInput

#!fsharp

let challengeInput =
    File.ReadAllLinesAsync "day 5 input.txt"
    |> Async.AwaitTask
    |> Async.RunSynchronously
    |> parseInput

lowestLocation challengeInput

#!markdown

## --- Part Two ---

Everyone will starve if you only plant such a small number of seeds. Re-reading the almanac, it looks like the seeds: line actually describes ranges of seed numbers.

The values on the initial seeds: line come in pairs. Within each pair, the first value is the start of the range and the second value is the length of the range. So, in the first line of the example above:

```
seeds: 79 14 55 13
```

This line describes two ranges of seed numbers to be planted in the garden. The first range starts with seed number 79 and contains 14 values: 79, 80, ..., 91, 92. The second range starts with seed number 55 and contains 13 values: 55, 56, ..., 66, 67.

Now, rather than considering four seed numbers, you need to consider a total of 27 seed numbers.

In the above example, the lowest location number can be obtained from seed number 82, which corresponds to soil 84, fertilizer 84, water 84, light 77, temperature 45, humidity 46, and location 46. So, the lowest location number is 46.

Consider all of the initial seed numbers listed in the ranges on the first line of the almanac. What is the lowest location number that corresponds to any of the initial seed numbers?

#!markdown

Let's do a really naieve approach for now - list out all seeds and determine where they go. If we were being smart, we would determine when each range starts and ends within the next transform step.

#!fsharp

let seedRange = function
| [| a; b |] -> { a.value .. a.value + b.value - 1L }
| _ -> raise (InvalidOperationException "Should never happen")

let allSeeds (input: Item array) =
    input
    |> Array.chunkBySize 2
    |> Seq.collect seedRange

sampleInput.initialSeeds |> allSeeds |> Seq.length

#!fsharp

challengeInput.initialSeeds |> allSeeds |> Seq.length

#!markdown

Took 20s. There are 2104769314 seeds to consider.

I tried the below:
```f#
challengeInput.initialSeeds
|> allSeeds
|> Seq.map (fun i -> { value = i; itemType = "seed" })
|> Seq.map (finalLocation challengeInput.maps)
|> Seq.min
```

however, this had not completed after 4 mins. I need to do this the smart way.

#!fsharp

[<Struct>]
type Range = {
    start: int64
    length: int64
}

type ItemRange = {
    itemType: string
    ranges: Range list
}

// Returns a tuple of (convertedRange: Range option, stillToBeConvertedRanges: Range list)
// The stillToBeConvertedRanges may end up with up to two entries for a single input, if
// it gets split in two
let mapInputRange (mapRange: MapRange) (input: Range) =
    // Note - start values are inclusive; end values are exclusive
    let iStart = input.start
    let iEnd = input.start + input.length
    let rStart = mapRange.sourceStart
    let rEnd = mapRange.sourceStart + mapRange.length
    
    // input falls completely outside map
    if iEnd <= rStart || iStart >= rEnd then (
        None,
        [ input ]
    )
    // input falls completely inside map
    else if iStart >= rStart && iEnd <= rEnd then (
        Some { start = iStart - rStart + mapRange.destinationStart; length = input.length },
        []
    )
    // input overlaps both left and right bounds of map
    else if iStart < rStart && iEnd > rEnd then (
        Some { start = mapRange.destinationStart; length = mapRange.length },
        [ { start = iStart; length = rStart - iStart }; { start = rEnd; length = iEnd - rEnd } ]
    )
    // input overlaps left bounds of map
    else if iStart < rStart then (
        Some { start = mapRange.destinationStart; length = iEnd - rStart },
        [ { start = iStart; length = rStart - iStart }]
    )
    // input overlaps right bounds of map
    else if iEnd > rEnd then (
        Some { start = iStart - rStart + mapRange.destinationStart; length = rEnd - iStart },
        [ { start = rEnd; length = iEnd - rEnd }]
    )
    // Error case
    else raise (new InvalidOperationException $"Unexpected bounds condition converting range { input } with map { mapRange }")

// Tests
[|
    { start = 50L; length = 10L }
    { start = 50L; length = 11L }
    { start = 49L; length = 10L }
    { start = 49L; length = 11L }
    { start = 49L; length = 12L }
    { start = 51L; length = 8L }
    { start = 51L; length = 9L }
    { start = 51L; length = 10L }
    { start = 49L; length = 8L }
    { start = 49L; length = 9L }
    { start = 60L; length = 1L }
    { start = 49L; length = 1L }
|]
|> Array.map (mapInputRange { sourceStart = 50L; length = 10L; destinationStart = 20L })

#!fsharp

let rec nextSetOfRanges (mapRanges: Span<MapRange>) (outputRanges: Range list) (inputRanges: Range list) =
    if mapRanges.IsEmpty then inputRanges@outputRanges
    else if inputRanges.IsEmpty then outputRanges
    else
        let (nextOutputs, nextInputs) =
            inputRanges
            |> List.map (mapInputRange mapRanges[0])
            |> List.unzip

        nextSetOfRanges (mapRanges.Slice(1)) (List.choose Operators.id nextOutputs@outputRanges) (List.concat nextInputs)

let toItemRange (input: Item array) =
    {
        itemType = input[0].itemType
        ranges =
            Seq.chunkBySize 2 input
            |> Seq.map (function
                | [| a; b |] -> { start = a.value; length = b.value }
                | _ -> raise (InvalidOperationException "Item array did not consist of pairs of values")
            )
            |> List.ofSeq
    }

let sampleSeeds = toItemRange sampleInput.initialSeeds
nextSetOfRanges (sampleInput.maps["seed"].ranges.AsSpan()) [] sampleSeeds.ranges

#!markdown

I think that looks right, let's try the final output

#!fsharp

let allStagesForSeedRange (mappings: Map<string, ChallengeMap>) (itemRange: ItemRange) =
    seq {
        let mutable current = itemRange
        yield current
        while mappings.ContainsKey current.itemType do
            current <- {
                itemType = mappings[current.itemType].destinationType
                ranges = (nextSetOfRanges (mappings[current.itemType].ranges.AsSpan()) [] current.ranges)
            }
            yield current
    }

let startOfItemRange (range: Range) = range.start

let lowestSoilForSeedRange (input: ChallengeInput) =
    let soilRanges =
        toItemRange input.initialSeeds
        |> allStagesForSeedRange input.maps
        |> Seq.last

    soilRanges.ranges
    |> Seq.map (fun x -> x.start)
    |> Seq.min

lowestSoilForSeedRange sampleInput

#!fsharp

lowestSoilForSeedRange challengeInput
